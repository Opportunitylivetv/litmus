LISA LISA2Rt1G
(*
 * Result: Never
 *
 * One RCU grace period and two RCU read-side critical sections, however,
 * these two critical sections are contained in the same thread.  They
 * are not nested, however, if the second one precedes a given grace
 * period, the first must do so as well, and vice versa.  It is possible
 * for the first to precede a grace period and the second to follow that
 * same grace period, but that won't allow a cycle in this case.
 * Cycle prohibited.
 *)
{
x0 = 0;
x1 = 0;
}
 P0                 | P1            ;
 f[rcu_read_lock]   | r[once] r1 x1 ;
 w[once] x0 1       | f[sync]       ;
 f[rcu_read_unlock] | r[once] r2 x0 ;
 f[rcu_read_lock]   |               ;
 w[once] x1 1       |               ;
 f[rcu_read_unlock] |               ;
exists (1:r1=1 /\ 1:r2=0)
